plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def groupIdStr = "com.whiner.kit"
def artifactIdStr = "tv_kit"
def versionStr = "1.0"

android {
    namespace 'com.whiner.kit'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33

        aarMetadata {
            minCompileSdk = 19
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

}

dependencies {

    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.8.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'

}

publishing {
    publications {
        release(MavenPublication) {
            groupId = groupIdStr
            artifactId = artifactIdStr
            version = versionStr

            afterEvaluate {
                from components.release
            }
        }
    }
}